struct VertexOutput {
    @builtin(position) position: vec4<f32>,
    @location(0) uv: vec2<f32>,
}

@vertex
fn vs_main(@builtin(vertex_index) in_vertex_index: u32) -> VertexOutput {
    var out: VertexOutput;
    var pos = array<vec2<f32>, 4>(
        vec2<f32>(-1.0, 1.0),
        vec2<f32>(-1.0, -1.0),
        vec2<f32>(1.0, 1.0),
        vec2<f32>(1.0, -1.0)
    );
    var uvs = array<vec2<f32>, 4>(
        vec2<f32>(0.0, 0.0),
        vec2<f32>(0.0, 1.0),
        vec2<f32>(1.0, 0.0),
        vec2<f32>(1.0, 1.0)
    );

    out.position = vec4<f32>(pos[in_vertex_index], 0.0, 1.0);
    out.uv = uvs[in_vertex_index];
    return out;
}

@group(0) @binding(0) var t_diffuse: texture_2d<f32>;
@group(0) @binding(1) var s_diffuse: sampler;

struct WindowOptions {
    opacity: f32,
    // Non-zero when the surface is CompositeAlphaMode::PreMultiplied, in which case rgb must
    // be pre-scaled by alpha. Otherwise (PostMultiplied, or Opaque where alpha is ignored by
    // the compositor entirely) rgb is emitted straight.
    premultiply: u32,
}
@group(1) @binding(0) var<uniform> options: WindowOptions;

@fragment
fn fs_main(in: VertexOutput) -> @location(0) vec4<f32> {
    let color = textureSample(t_diffuse, s_diffuse, in.uv);
    let alpha = color.a * options.opacity;
    if options.premultiply != 0u {
        return vec4<f32>(color.rgb * alpha, alpha);
    }
    return vec4<f32>(color.rgb, alpha);
}
